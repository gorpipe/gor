/*
 *  BEGIN_COPYRIGHT
 *
 *  Copyright (C) 2011-2013 deCODE genetics Inc.
 *  Copyright (C) 2013-2021 WuXi NextCode Inc.
 *  All Rights Reserved.
 *
 *  GORpipe is free software: you can redistribute it and/or modify
 *  it under the terms of the AFFERO GNU General Public License as published by
 *  the Free Software Foundation.
 *
 *  GORpipe is distributed "AS-IS" AND WITHOUT ANY WARRANTY OF ANY KIND,
 *  INCLUDING ANY IMPLIED WARRANTY OF MERCHANTABILITY,
 *  NON-INFRINGEMENT, OR FITNESS FOR A PARTICULAR PURPOSE. See
 *  the AFFERO GNU General Public License for the complete license terms.
 *
 *  You should have received a copy of the AFFERO GNU General Public License
 *  along with GORpipe.  If not, see <http://www.gnu.org/licenses/agpl-3.0.html>
 *
 *  END_COPYRIGHT
 */

import com.nextcode.gradle.*

plugins {
    id "gor.java-internal-library"
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
}

// Use shadow jar to create jar with the content of the module and its 'internal' dependencies.  If the module
// as no 'internal' dependenceis this will behave just like normal jar task.    Note, only the internal top level
// dependencies are included, but none of external/standard dependencies.

configurations.internal.transitive = false // Internal are not transitive, but deps are added as dependencies.

shadowJar {
    manifest manifestTemplate
    archiveBaseName = getProjectFullName()
    archiveClassifier = ''
    mergeServiceFiles()
    configurations = [project.configurations.internal]    // Only collect internals.
}

// Replace std jar with shadowJar.
assemble.dependsOn(shadowJar)
jar {
    enabled = false
    dependsOn(shadowJar)
}
// Workaround for check input publish plugin, replace jar with shadowJar.
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(shadowJar)

    }
}

publishing {
    publications {
        mavenJavaLibModule(MavenPublication) {  MavenPublication publication ->
            artifactId = getCustomProjectId()
            groupId = getCustomGroupId()
            // Call this method to create correct pom (that excludes the 'internal' dependencies, but add all the
            // needed external/standard dependencies).
            CorrectShadowPublishing.configure(publication, project)
        }
    }
    repositories {
        if (project.hasProperty('publishToGitlab')) {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/gorpipe/gor"
                credentials {
                    username = project.findProperty('github_user') ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty('github_token') ?: System.getenv("GITHUB_TOKEN")
                }
            }
        } else {
            maven {
                name "gitlab-gor-maven"
                url uri("https://gitlab.com/api/v4/projects/17033400/packages/maven")
                credentials(HttpHeaderCredentials) {
                    name = project.findProperty('gitlab_token') ? "Private-Token" : "Job-Token"
                    value = project.findProperty('gitlab_token') ?: System.getenv("CI_JOB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}
